apply plugin: 'com.android.application'

android {
    compileSdkVersion 22
    buildToolsVersion "21.1.2"

    defaultConfig {
        applicationId "org.michenux.yourappidea"
        minSdkVersion 10
        targetSdkVersion 22
        versionCode 210
        versionName "2.1.0"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    signingConfigs {
        release {
            if (project.hasProperty('yourappidea.keystore')) {
                storeFile file(project.property("yourappidea.keystore"))
                storePassword project.property("yourappidea.keystore.password")
                keyAlias project.property("yourappidea.keystore.keyAlias")
                keyPassword project.property("yourappidea.keystore.keyPassword")
            }
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationIdSuffix ".debug"
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (project.hasProperty('yourappidea.keystore')) {
                signingConfig signingConfigs.release
            }
        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:22.2.0'
    compile 'com.android.support:cardview-v7:22.2.0'
    compile 'de.keyboardsurfer.android.widget:crouton:1.8.5@aar'
    compile 'com.nostra13.universalimageloader:universal-image-loader:1.9.4'
    compile 'fr.avianey:facebook-android-api:3.18.0@aar'
    compile 'com.github.traex.rippleeffect:library:1.3'
    compile 'org.lucasr.twowayview:core:1.0.0-SNAPSHOT@aar'
    compile 'org.lucasr.twowayview:layouts:1.0.0-SNAPSHOT@aar'
    compile project(':drodrolib')
}
